#!/usr/bin/env spang2
# @endpoint https://orth.dbcls.jp/sparql-dev
# @option -vac

PREFIX orth: <http://purl.org/net/orth#>
PREFIX homologene: <https://ncbi.nlm.nih.gov/homologene/>
PREFIX taxid: <http://identifiers.org/taxonomy/>
PREFIX hop: <http://purl.org/net/orthordf/hOP/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?branch_time_mya (COUNT(?human_gene) AS ?gene_count) ((xsd:decimal(?gene_count) / ?total_genes * 100) AS ?percent)
WHERE {
  {
    SELECT ?human_gene (max(?time) as ?branch_time_mya)
    WHERE {
      ?grp orth:inDataset homologene: ;
          orth:hasHomologousMember ?human_gene, ?gene .
      ?human_gene orth:taxon taxid:9606 .
      ?gene orth:taxon ?taxid .
      ?taxid hop:branchTimeMya ?time .
    }
  }
  {
    SELECT (COUNT(DISTINCT ?human_gene) AS ?total_genes)
    WHERE {
      ?grp orth:inDataset homologene: ;
          orth:hasHomologousMember ?human_gene .
      ?human_gene orth:taxon taxid:9606 .
    }
  }
}
ORDER BY ?branch_time_mya
